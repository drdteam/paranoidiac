//==================
// Alien Controller
//==================

class HLController : HLMonster
{
	Default
	{
		Health 200;
		Mass 200;
		Speed 2.666667;
		FloatSpeed 1.333333;
		Height 56;
		Radius 23;
		PainChance 64;
		PainChance "Crowbar",210;
		+NOGRAVITY
		+FLOAT
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/controller/Sight";
		PainSound "halflife/monsters/controller/Pain";
		DeathSound "halflife/monsters/controller/Death";
		ActiveSound "halflife/monsters/controller/idle";
		Species "Xen";
	}
	States
	{
	See:
		HLI1 A 1 A_Chase;
		HLI1 AA 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLA1 A 0
		{
			A_PlaySound("halflife/monsters/controller/attack");
			switch(random(0,2))		//equal chance to go right, left or not move at all
			{
			case 1:
				vel.y+=random(5,8);
				break;
			case 2:
				vel.y-=random(5,8);
				break;
			}
		}
		HLA1 A 0 A_Jump(128,"Missile2");
		HLA1 ABDGJMPSVYZ\ 1 A_FaceTarget;
		HLA2 BBBBBB 5 A_SpawnProjectile("ControllerFireball1",32,0,random(10,-10));
		HLA2 F 1 A_Stop;
		HLA2 JNR 1;
		Goto See;
	Missile2:
		HLA2 WXYZ[\] 1 A_FaceTarget;
		HLA3 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1 A_FaceTarget;
		HLA4 ABCDEFGH 1;
		HLA4 I 1 A_SpawnProjectile("ControllerFireball2",42);
		HLA4 JKLMNOPQRSTUVWXYZ[\] 1;
		HLA5 ABC 1;
		HLA5 D 1 A_Stop;
		HLA5 EF 2;
		Goto See;
	Pain:
		HLP1 ABCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 2
		{
			angle+=90;
			A_Scream();
		}
		HLD1 BC 2;
		Wait;
	Crash:
		HLD1 D 1 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 EFGHIJKLMNO 1;
		HLD1 P 1 A_NoBlocking;
		HLD1 QRSTUVWXYZ[\] 1;
		HLD2 ABCDEFGHIJKLMNOPQRS 1;
		HLD2 T -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(17,"XenGibs");
		Stop;
	}
}

class ControllerFireball1 : Actor
{
	Default
	{
		Projectile;
		Speed 18;
		Damage 3;
		Radius 8;
		Height 12;
		Scale .5;
		RenderStyle "Add";
		DeathSound "halflife/weapons/gausshit2";
		Alpha .9;
	}
	States
	{
	Spawn:
		X029 ABCDEFGHIJK 2 Bright LIGHT("YellowMediumPoint3");
		Loop;
	Death:
		TNT1 A 5 LIGHT("YellowSmallFlicker1")
		{
			for(int i=0;i<6;i++)
				A_SpawnItemEx("ControllerFireballSpark",0,0,0,frandom(-1.1,1.1),frandom(-1.1,1.1),frandom(-1.1,1.1),random(0,359));
		}
		TNT1 A 5 LIGHT("YellowSmallFlicker2");
		TNT1 A 5 LIGHT("YellowSmallFlicker3");
		Stop;
	}
}
class ControllerFireballSpark : Actor
{
	float fade;
	Default
	{
		Radius 2;
		Height 2;
		+NOINTERACTION
		Scale .02;
		RenderStyle "Add";
		+FORCEXYBILLBOARD
		Translation "80:111=160:167";
	}
	states
	{
	Spawn:
		X099 A 0 NODELAY {fade=random(3,5)/100.0;}
		X099 A 1 A_FadeOut(fade);
		Wait;
	}
}

class ControllerFireball2 : ControllerFireball1
{
	Default
	{
		Speed 10;
		Damage 12;
		Scale .4;
		DeathSound "halflife/weapons/gausshit1";
		+SEEKERMISSILE
	}
	States
	{
	Spawn:
		X025 BCDEFGHIJKLMNOPQRSTUVWXY 2 Bright LIGHT("OrangeMediumPoint2") A_SeekerMissile(5,5);
		Loop;
	Death:
		TNT1 A 5 LIGHT("OrangeMediumFlicker3")
		{
			for(int i=0;i<8;i++)
				A_SpawnItemEx("ControllerFireball2Spark",0,0,0,frandom(-0.7,0.7),frandom(-0.7,0.7),frandom(-0.7,0.7),random(0,359));
		}
		TNT1 A 5 LIGHT("OrangeSmallFlicker1");
		TNT1 A 5 LIGHT("OrangeSmallFlicker2");
		Stop;
	}
}
class ControllerFireball2Spark : ControllerFireballSpark
{
	Default
	{
		Scale .3;
	}
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,1,3,5,7,9,11,13,15);
	Looper:
		X025 BCDEFGHIJKLMNOPQRSTUVWXY 2 Bright A_FadeOut(.15);
		Loop;
	}
}