///====================
// Vortigaunt (Slave)
//====================

class HLVortigaunt : HLMonster
{
	Default
	{
		Health 175;
		Mass 200;
		Speed 3.5;
		Height 63;
		PainChance 100;
		PainChance "Crowbar",243;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/slave/sight";
		PainSound "halflife/monsters/slave/pain";
		DeathSound "halflife/monsters/slave/death";
		ActiveSound "halflife/monsters/slave/idle";
		maxdropoffheight 64;
		Species "Xen";
	}
	States
	{
	See:
		HLR1 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_HLChase;
		Loop;
	Melee:
		HLI1 A 4 A_FaceTarget;
		HLA1 ABCDEFGH 1 A_FaceTarget;
		HLA1 I 1 A_ZombieMelee(3);
		HLA1 JKL 1 A_FaceTarget;
		HLA1 M 1 A_ZombieMelee(3);
		HLA1 NOPQRSTU 1 A_FaceTarget;
		HLA1 V 1 A_ZombieMelee(3);
		HLA1 WXYZ[\] 1;
		HLA2 ABCD 1;
		Goto See;
	Missile:
		HLA2 E 2
		{
			A_StartSound("halflife/weapons/zap4", CHAN_5);
			A_VortiLightning();
		}
		HLA2 F 2;
		HLA2 G 2 A_VortiLightning;
		HLA2 H 2;
		HLA2 I 2 A_VortiLightning;
		HLA2 J 2;
		HLA2 K 2 A_VortiLightning;
		HLA2 L 2;
		HLA2 M 2 A_VortiLightning;
		HLA2 O 2;
		HLA2 P 2 A_VortiLightning;
		HLA2 Q 2;
		HLA2 R 2 A_VortiLightning;
		HLA2 S 2;
		HLA2 T 2 A_VortiLightning;
		HLA2 U 2;
		HLA2 V 2
		{
			A_StartSound("halflife/weapons/zap4", CHAN_5);
			A_VortiLightning();
		}
		HLA2 W 2;
		HLA2 X 2 A_VortiLightning;
		HLA2 Y 2;
		HLA2 Z 2 A_VortiLightning;
		HLA2 [ 2;
		HLA2 \ 2 A_VortiLightning;
		HLA2 ] 2;
		HLA3 A 2 A_VortiLightning;
		HLA3 B 2;
		HLA3 C 2
		{
			A_StartSound("halflife/weapons/gausshit1", CHAN_5);
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_SpawnProjectile("VortiPlasma",32,9);
			A_SpawnProjectile("VortiPlasma",32,-9);
		}
		HLA3 DEFGHI 2;
		Goto See;
	Pain:
		HLP1 A 1
		{
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_Pain();
		}
		HLP1 BCDEFG 1;
		Goto See;
	Death:
		HLD1 A 2
		{
			A_KillChildren(flags:KILS_KILLMISSILES);
			A_Scream();
		}
		HLD1 B 2 A_NoBlocking;
		HLD1 CDEF 2;
		HLD1 G 2 A_StartSound("halflife/weapons/bodydrop");
		HLD1 HIJKLMNOPQRSTUVWXYZ[\] 2;
		HLD2 A -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0
		{
			A_HLGibs(14,"XenGibs");
			A_KillChildren(flags:KILS_KILLMISSILES);
		}
		Stop;
	}
	void A_VortiLightning()
	{
		A_FaceTarget();
		A_SpawnItemEx("VortiLightning",flags:SXF_SETMASTER|SXF_NOCHECKPOSITION);
	}
}

//	The regular non model attack
class VortiLightning : Actor
{
	Default
	{
		Height 1;
		Radius 1;
		+MISSILE
		+NOCLIP
		RenderStyle "Add";
		Alpha .7;
		Scale 1.05;
	}
	States
	{
	Spawn:
		X098 A 2 Bright LIGHT("GreenMediumFlicker2") NODELAY {self.frame=random(0,7)*2;}
		XO98 A 2 Bright LIGHT("GreenMediumFlicker2") {self.frame++;}
		Stop;
	Death:
		TNT1 A 1;
		Stop;
	}
}

//====================
// The model attack
//====================

class VortiPlasma_Bolt : Actor
{
	Default
	{
		+NOGRAVITY
		RenderStyle "Add";
		Alpha .75;
		Scale .3;
	}
	States
	{
	Spawn:
		VPLS A 0 NODELAY {self.frame=random(0,3);}
		VPLS # 1 A_FadeOut;
		Wait;
	}
}

class VortiPlasma : VortiPlasma_Bolt
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 36;
		DamageFunction (random(1,4)*10);
		Projectile;
		+RANDOMIZE
		Alpha .55;
		DeathSound "halflife/weapons/gaussshot";
	}
	States
	{
	Spawn:
		VPLS FG 6 BRIGHT LIGHT("GreenMediumFlicker3");
		Loop;
	Death:
		VPLS A 3 BRIGHT LIGHT("GreenMediumFlicker3") LIGHT("GreenMediumFlicker2") A_SpawnItemEx("VortiPlasma_Bolt");
		VPLS B 3 BRIGHT LIGHT("GreenMediumFlicker3");
		VPLS C 3 BRIGHT LIGHT("GreenSmallFlicker1");
		VPLS D 3 BRIGHT LIGHT("GreenSmallFlicker2");
		VPLS E 3 BRIGHT LIGHT("GreenSmallFlicker3");
		Stop;
	}
}