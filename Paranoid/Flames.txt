class OMGFire : Actor
{
	Default
	{
		+NOGRAVITY
		+NOBLOCKMAP
		+NOCLIP
		+NOSECTOR
		+LOOKALLAROUND
	}
	States
	{
	Spawn:
		TNT1 A 1 NODELAY
		{
			if(CheckSightOrRange(2560))
			{
				A_SetTics(10);
				return;
			}
			int area = args[0], shape = args[1], glow = args[2], t;
			float s, a, r, xval, yval, zval = pos.z;
			for(int i = 0; i < area / 32 + 1; i++)
			{
				s = frandom(.1, .3);
				t = random(2, 3);
				switch(shape)
				{
				case 0:
					a = frandom(0.0, 1.0);
					r = frandom(0, area / 2);
					xval = pos.x + cos(a) * r;
					yval = pos.y + sin(a) * r;
					a *= 360;
					break;
				case 1:
					a = random(0, 360);
					xval = pos.x + frandom(-area / 2, area / 2);
					yval = pos.y + frandom(-area / 2, area / 2);
					break;
				case 2:
					a = angle;
					r = frandom(0, area);
					xval = pos.x + cos(a) * r;
					yval = pos.y + sin(a) * r;
					break;
				}
				let thisflame = Spawn("Flame1A_01", (xval, yval, zval));
				thisflame.vel.z += t;
				thisflame.angle = a;
				thisflame.A_SetScale(s);
				if(glow == 1 || glow == 3)
				{
					let thisglow = Spawn("FlameGlow_02", (xval, yval, zval));
					thisglow.angle = a;
					thisflame.A_SetScale(s * 2.0);
				}
				if(glow >= 2)
				{
					let thatglow = Spawn("FlameGlow_02", (xval, yval, zval));
					thatglow.angle = a;
					thatglow.vel.z += t;
					thisflame.A_SetScale(s * 2.0);
				}
				if(!random(0, 64))
				{
					let thisember = Spawn("Ember", (xval, yval, zval));
					thisember.angle = random(0, 359);
					thisember.vel.z += random(3, 6);
					thisember.vel.x += random(-1, 1);
					thisember.vel.y += random(-1, 1);
				}
			}
		}
		Loop;
	}
}

class Ember : Actor
{
	Default
	{
		Height 2;
		Radius 1;
		Projectile;
		RenderStyle "Add";
		Scale .0625;
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		SPKO E 35 BRIGHT;
		SPKO E 1 BRIGHT A_FadeOut;
		Wait;
	}
}

class FlameGlow_02 : Ember
{
	Default
	{
		Height 1;
		Radius 8;
		+NOCLIP
		Alpha .25;
		Scale .2;
	}
	States
	{
	Spawn:
		SPKO E 1 BRIGHT A_FadeOut(.0125);
		Loop;
	}
}

class Flame1A_01 : FlameGlow_02
{
	float startscale, angmod;
	Default
	{
		-NOCLIP
		DamageFunction (1);
		DamageType "Fire";
		+RIPPER
		+BLOODLESSIMPACT
		+WINDTHRUST
		+NOINTERACTION
		+ROLLSPRITE
		Alpha 1;
		Scale .1;
	}
	States
	{
	Spawn:
		FLM1 A 1 NODELAY
		{
			startscale = scale.x / 26.0;
			bSPRITEFLIP = random(0, 1);
			frame = random(0, 4);
			switch(random(0, 2))
			{
			case 1:
				angmod = 7.5;
				break;
			case 2:
				angmod = -7.5;
				break;
			}
		}
		FLM1 ################### 1 BRIGHT A_FlameRoll;
		FLM1 ###### 1 BRIGHT
		{
			A_FlameRoll();
			A_FadeOut(.2);
		}
		Stop;
	}
	void A_FlameRoll()
	{
		scale.x -= startscale;
		scale.y -= startscale;
		A_SetRoll(roll += angmod, SPF_INTERPOLATE);
	}
}