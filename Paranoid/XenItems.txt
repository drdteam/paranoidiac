class HLXenHair : ModelBase
{
	override void HLModelSetup()
		{A_ChangeModel("", 0, "Models", "hair.iqm", 0, "Models", "hair.png");}
	Default
	{
		Radius 4;
		Height 32;
		-CANPASS
		-SOLID
		+RANDOMIZE
		+DONTTHRUST
		+DECOUPLEDANIMATIONS
	}
	States
	{
	Spawn:
		HLI1 A -1 NODELAY SetAnimation('spin', 30, flags: SAF_LOOP|SAF_NOOVERRIDE);
		Stop;
	}
}

class HLXenFungus : HLXenHair
{
	override void HLModelSetup()
	{
		static const name modelskin[]=
		{
			'fungus_000.png',
			'fungus_001.png',
			'fungus_002.png'
		};
		for(int i = 0; i <= 2; i++)
			A_ChangeModel("", 0, "Models", "fungus.iqm", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
	}
	Default
	{
		Scale .5;
		Radius 8;
		+NOTAUTOAIMED
		+SHOOTABLE
		+NOBLOOD
		+NODAMAGE
		+SOLID
	}
	States
	{
	Spawn:
		HLI1 A -1 NODELAY SetAnimation('idle1', 30, flags: SAF_LOOP|SAF_NOOVERRIDE);
		Stop;
	}
}

class HLXenLight : HLXenFungus
{
	override void HLModelSetup()
		{A_ChangeModel("", 0, "Models", "light.iqm", 0, "Models", "light.png");}
	Default
	{
		Scale 1;
		PainChance 256;
	}
	States
	{
	Spawn:
		HLI1 A 1 NODELAY Bright LIGHT("XENLIGHT1")
		{
			SetAnimation('idle1', 30, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_HLXenLightLook();
		}
		Loop;
	Pain:
		HLI1 A 10 LIGHT("XENLIGHTFADE2") SetAnimation('retract', 60, endframe: 18);
		HLI1 A 10 A_CheckRange(128, 1);
		Wait;
		HLI1 A 73 LIGHT("XENLIGHTFADE2") SetAnimation('delpoy', 40, endframe: 73);
		Goto Spawn;
	}
	void A_HLXenLightLook()
	{
		if(!CheckRange(128))
			SetStateLabel("Pain");
	}
}

class HLXenTree : HLXenFungus
{
	override void HLModelSetup()
		{A_ChangeModel("", 0, "Models", "tree.iqm", 0, "Models", "tree.png");}
	Default
	{
		Scale 1;
		Radius 20;
		Height 144;
		MeleeRange 128;
		PainChance 128;
		PainChance "Crowbar",252;
		PainSound "tree/pain";
	}
	States
	{
	Spawn:
		HLI1 A 2
		{
			SetAnimation('idle1', 20, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_HLXenTreeLook();
		}
		Loop;
	Melee:
		HLI1 A 10 SetAnimation('attack', 35);
		HLI1 A 21 A_HLXenTreeAttack;
		Goto Spawn;
	Pain:
		HLI1 A 12
		{
			A_Pain();
			SetAnimation('attack', 17.5, endFrame: 6);
		}
		Goto Spawn;
	}
	void A_HLXenTreeLook()
		{A_LookEx(LOF_NOSOUNDCHECK,0,112,112,60,"Melee");}
	void A_HLXenTreeAttack()
	{
		for(int i=0;i<12;i++)
		{
			if(TryPunch(angle+i*(45./12))||TryPunch(angle-i*(45./12)))
				return;
		}
	}
	private action bool TryPunch(double angle)
	{
		FTranslatedLineTarget t;
		double slope=AimLineAttack(angle,112,t);
		if(t.linetarget!=null)
		{
			LineAttack(angle,112,slope,10,'Melee',"BulletPuff",true,t);
			if(t.linetarget!=null)
			{
				A_StartSound("tree/attack");
				return true;
			}
		}
		A_StartSound("tree/swipe");
		return false;
	}
}

class DissectedHeadCrab : ModelBase
{
	override void HLModelSetup()
	{
		static const name modelskin[]=
		{
			'dissected_headcrab_000.png',
			'dissected_headcrab_001.png',
			'dissected_headcrab_002.png',
			'dissected_headcrab_003.png'
		};
		for(int i = 0; i <= 3; i++)
			A_ChangeModel("ModelBase", 0, "Models", "dissected_headcrab.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
	}
	Default
	{
		Radius 8;
		Height 16;
		Mass 100;
		+NOTAUTOAIMED
		+SHOOTABLE
		+NOBLOOD
		+NODAMAGE
	}
}

class XenIsland1 : ModelBase
{
	override void HLModelSetup()
		{A_ChangeModel("", 0, "Models", "island.md3", 0, "Models", "island.png");}
	Default
	{
		Scale 500;
		Radius 96;
		Height 128;
		-SOLID
		+NOGRAVITY
	}
}
class XenIsland2 : XenIsland1
{
	override void HLModelSetup()
		{A_ChangeModel("XenIsland1", 0, "Models", "island.md3", 0, "Models", "xenrock2.png");}
}
class XenIsland3 : XenIsland1
{
	override void HLModelSetup()
		{A_ChangeModel("XenIsland1", 0, "Models", "island.md3", 0, "Models", "rock_big.jpg");}
	Default
	{
		Scale 1800;
		Radius 288;
		Height 384;
	}
}
class XenIsland4 : XenIsland3
{
	override void HLModelSetup()
		{A_ChangeModel("XenIsland1", 0, "Models", "island.md3", 0, "Models", "rock_moss1.jpg");}
}
class XenIsland5 : XenIsland3
{
	override void HLModelSetup()
		{A_ChangeModel("XenIsland1", 0, "Models", "island.md3", 0, "Models", "rock_big2.jpg");}
}
class XenIsland6 : XenIsland3
{
	override void HLModelSetup()
		{A_ChangeModel("XenIsland1", 0, "Models", "island.md3", 0, "Models", "rock_moss2.jpg");}
}