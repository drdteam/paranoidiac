class OMGSteam : SwitchableDecoration
{
	Default
	{
		Height 2;
		Radius 1;
		+NOCLIP
		+NOSECTOR
		+NOGRAVITY
		+NOBLOCKMAP
		+LOOKALLAROUND
	}
	States
	{
	Spawn:
	Active:
		TNT1 A 1 NODELAY
		{
			A_PlaySound("Steam/Loop",7,1,1);
			if(CheckSightOrRange(2560))
			{
				A_SetTics(10);
				return;
			}
			bool num;
			actor me;
			[num,me]=A_SpawnItemEx("Steam1",0,0,0,
			(0.1)*steamscript(args[0],args[1],0),
			(0.1)*steamscript(args[0],args[1],1),
			(0.1)*steamscript(args[0],args[1],2));
			me.scale.x=(args[0]+1)/10.0;
			me.scale.y=me.scale.x;
		}
		Loop;
	Inactive:
		TNT1 A -1 A_StopSound(7);
		Stop;
	}
	float steamscript(int size,int thispitch,int plane)
	{
		float momentum;
		thispitch+=random(-4,4);
		float r=frandom(30.0,35.0);
		switch(plane)
		{
		case 0:
			momentum=cos(angle)*(cos(thispitch)*r);
			break;
		case 1:
			momentum=sin(angle)*(cos(thispitch)*r);
			break;
		case 2:
			momentum=-sin(thispitch)*r;
			break;
		}
		return momentum*(size+1)/5.0;
	}
}

class Steam1 : Actor
{
	Default
	{
		Height 2;
		Radius 1;
		+MISSILE
		+NOGRAVITY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha .65;
	}
	States
	{
	Spawn:
		STEM ABCDEFGHIJKL 2 A_FadeOut(.05);
		Stop;
	}
}