//===========
// Gargantua
//===========
class HLGargantua : HLMonster
{
	override void HLModelSetup()
	{
		A_ChangeModel("HLGargantua", 0, "Models", "garg.iqm", 0, "Models", "garg_000.png", CMDL_USESURFACESKIN);
		A_ChangeModel("HLGargantua", 0, "Models", "", 1, "Models", "garg_001.png", CMDL_USESURFACESKIN);
		SetModelFlag(MDL_INTERPOLATEDOUBLEDFRAMES);
	}
	bool flametoken;
	float defspeed;
	override void BeginPlay()
	{
		defspeed=speed;
		Super.BeginPlay();
	}
	Default
	{
		Health 10000;
		Mass 0x7FFFFFFF;
		Speed 21;
		Height 170;
		Radius 57;
		MaxStepHeight 48;
		MaxDropOffHeight 48;
		+BOSS
		+NORADIUSDMG
		PainChance 5;
		PainChance "Crowbar",29;
		DamageFactor "Hornet",0;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/gargantua/sight";
		PainSound "halflife/monsters/gargantua/pain";
		DeathSound "halflife/monsters/gargantua/death";
		ActiveSound "halflife/monsters/gargantua/idle";
		Species "Xen";
		HLMonster.WalkTic 3;
	}
	States
	{
	/*Spawn:
		HLI1 A 3
		{
			SetAnimation('idle3', 10, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_Look();
		}
		Loop;*/
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 3 A_Look;
		HLI2 AB 3 A_Look;
		Loop;
	See:
		HLR1 A 0 A_JumpIfCloser(400,"Walk");		//if is close enough, the gargantua will walk instead of run
		HLR1 A 0 A_SetSpeed(defspeed/3.0);
		HLR1 ABCDEFGHIJ 1 A_HLChase;
		HLR1 K 0 A_StartSound("halflife/monsters/gargantua/step", CHAN_7);
		HLR1 KLMNOPQRST 1 A_HLChase;
		HLR1 T 0 A_StartSound("halflife/monsters/gargantua/step", CHAN_7);
		Goto See+2;
	Walk:
		HLW1 A 0 A_SetSpeed(defspeed/5.0);
		HLW1 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 A_HLChase(5);
		HLW1 Q 0 A_StartSound("halflife/monsters/gargantua/step", CHAN_7);
		HLW1 QQRRSSTTUUVVWWXXYYZZ[[\\]] 1 A_HLChase(5);
		HLW2 AABBCC 1 A_HLChase(5);
		HLW2 C 0 A_StartSound("halflife/monsters/gargantua/step", CHAN_7);
		Goto See;
	Missile:
		TNT1 A 0 A_JumpIfCloser(250,"Flame");
		TNT1 A 0 A_Jump(256,"Boom","Hornets","EnergyBalls","EnergyBallsSeek");
	Boom:
		HLI1 A 8 A_FaceTarget;
		HLA1 ABCDEFGHIJKLMNOPQRS 1 A_FaceTarget;
		HLA1 T 1
		{
			A_StartSound("halflife/monsters/gargantua/stomp", CHAN_7, attenuation:ATTN_NONE);
			Radius_Quake(8,20,0,15,0);
			A_SpawnItemEx("GargFloorBoomFake",10);
		}
		HLA1 T 22;
		HLA1 T 1 A_HLGargFloorBoom;
		HLA1 SQOMKIGECA 1;
		Goto See;
	Hornets:
		HLA2 ABCD 2 A_FaceTarget;
		HLA2 EFGHIJKLMNOPSS 1 A_FaceTarget;
		HLA2 SSSSSSSSS 5 A_GargProjectile("HGruntHornet");
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	Flame:
		HLA2 A 1 A_StartSound("halflife/monsters/gargantua/flameon", CHAN_5);
		HLA2 BCDEFGHIJKLMNOP 1 A_FaceTarget;
		HLA2 S 0
		{
			A_StartSound("halflife/monsters/gargantua/flameloop", CHAN_6, CHANF_LOOPING);
			flametoken=1;
		}
	FlameLoop:
		HLA2 SSSSS 1 A_GargProjectile("HLGargFlame");
		HLA2 S 0 A_MonsterRefire(40,"FlameEnd");
		HLA2 S 0 A_JumpIfCloser(300,"FlameLoop");
	FlameEnd:
		HLA2 P 1
		{
			A_StopSound(6);
			A_StartSound("halflife/monsters/gargantua/flameoff", CHAN_5);
			flametoken=0;
		}
		HLA2 ONMLKJIHGFEDCBA 1;
		Goto See;
	EnergyBalls:
		HLA2 ABCDE 2 A_FaceTarget;
		HLA2 FGHI 1 A_FaceTarget;
		HLA2 J 1 A_GargDischarge;
		HLA2 KLMNOPSS 1 A_FaceTarget;
		HLA2 SSSSSSSSSSSSSSSS 5 A_GargControllerBall("GargControllerFireball1");
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	EnergyBallsSeek:
		HLA2 ABCDE 2 A_FaceTarget;
		HLA2 FGHI 1 A_FaceTarget;
		HLA2 J 1 A_GargDischarge;
		HLA2 KLMNOPSS 1 A_FaceTarget;
		HLA2 S 5 A_GargControllerBall;
		HLA2 SSSSSS 5 A_FaceTarget;
		HLA2 S 7 A_GargDischarge;
		HLA2 S 5 A_GargControllerBall;
		HLA2 SSSSSS 5 A_FaceTarget;
		HLA2 S 7 A_GargDischarge;
		HLA2 S 5 A_GargControllerBall;
		HLA2 SSPONMLKJIHGFEDCBA 1;
		Goto See;
	Pain:
		HLP1 A 0 A_Pain;
		HLP1 A 1 A_JumpIf(flametoken,"FlameEnd");
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		/*HLI1 A 140
		{
			SetAnimation('die', 15);
			A_StopSound(6);
			A_ScreamAndUnblock();
		}
		HLI1 A 110 A_StartSound("gonarch/step", CHAN_7);
		TNT1 A 10 A_XenGibsBig;
		Stop;*/
		HLD1 A 1
		{
			A_StopSound(6);
			A_Scream();
			A_KillChildren(flags: KILS_KILLMISSILES|KILS_NOMONSTERS);
		}
		HLD1 BCDE 1;
		HLD1 F 1 A_NoBlocking;
		HLD1 GHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLD2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLD3 ABCDEF 1;
		HLD3 G 1 A_StartSound("gonarch/step", CHAN_7);
		HLD3 HIJKLMNOP 1;
		HLD3 Q 100;
		TNT1 A 10 A_XenGibsBig;
		Stop;
	XDeath:
		HLD1 A 1
		{
			A_StopSound(6);
			A_Scream();
			A_KillChildren(flags: KILS_KILLMISSILES|KILS_NOMONSTERS);
		}
		HLD1 BCDEFGHIJKLMNOPQRSTUVWXY 1;
		HLD1 ZZZZZZZ 1 {scale.x+=.025;}
		TNT1 A 10 A_XenGibsBig;
		Stop;
	}
	void A_GargProjectile(class<actor> proj)
	{
		float a=80*scale.x,b=90*scale.x;
		A_FaceTarget();
		A_SpawnProjectile(proj,a,0-b);
		A_SpawnProjectile(proj,a,b);
	}
	void A_GargControllerBall(class<actor> ball="GargControllerFireball2")
	{
		A_FaceTarget();
		A_SpawnProjectile(ball,90,-66,random(8,-8));
		A_SpawnProjectile(ball,90,66,random(8,-8));
	}
	void A_GargDischarge()
	{
		A_FaceTarget();
		A_StartSound("halflife/weapons/shockroachdischarge");
	}
	virtual void A_HLGargFloorBoom()
	{
		let floorboom = A_SpawnProjectile("GargFloorBoomSeeker",0);	//aimed at target
		if(floorboom)
			floorboom.master = self;
	}
}

//=================
// Young Gargantua
//=================
class HLGargantua2 : HLGargantua
{
	override void HLModelSetup()
	{
		A_ChangeModel("HLGargantua", 0, "Models", "garg.iqm", 0, "Models", "garg_000.png", CMDL_USESURFACESKIN);
		A_ChangeModel("HLGargantua", 0, "Models", "", 1, "Models", "garg2_001.png", CMDL_USESURFACESKIN);
		SetModelFlag(MDL_INTERPOLATEDOUBLEDFRAMES);
	}
	Default
	{
		Health 8000;
		Speed 18;
		Height 128;
		Radius 40;
		Scale .75;
	}
	States
	{
	Missile:
		TNT1 A 0 A_JumpIfCloser(250,"Flame");
		TNT1 A 0 A_Jump(256,"Boom","Hornets");
	}
	override void A_HLGargFloorBoom()
		{A_SpawnItemEx("GargFloorBoom",10,0,0,20);}	//where it aimed first
}

//********************
//	Flame attack
//********************
class HLGargFlame : Actor
{
	Default
	{
		Radius 2;
		Height 4;
		Speed 16;
		Projectile;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha .67;
		Scale .5;
		+DONTSPLASH
		SeeSound "gargantua/flamestrm";
		Damage 1;
		Translation "48:63=200:193","64:79=213:220";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		FRFX ABCD 1 Bright A_Explode(3,6);
		FRFX D 0 A_Lowgravity;
		FRFX EFG 1 Bright A_Explode(3,12);
		FRFX HIJ 1 Bright A_Explode(3,24);
		FRFX KLM 1 Bright A_Explode(3,48);
		FRFX NO 1 Bright;
		stop;
	Death:
		FRFX HIJ 2 Bright A_Explode(3,24);
		FRFX KLM 2 Bright A_Explode(3,48);
		FRFX NO 2 Bright;
		stop;
	}
}

//*******************
//	Floor attack
//*******************
class GargFloorBoomFake : Actor
{
	Default
	{
		Projectile;
		Damage 15;
		+RIPPER
		Scale .65;
		+FLOORHUGGER
		RenderStyle "Add";
		Alpha .9;
	}
	States
	{
	Spawn:
		X036 BCDEFGHIJKLMNOPQRSTUVWXY 1 Bright LIGHT("RedMediumPoint2") NODELAY A_GargFXtra;
		TNT1 A 0
		{
			if(speed==0)
				return resolvestate("Death");
			return state(null);
		}
		Loop;
	Death:
		TNT1 A 1;
		Stop;
	}
	void A_GargFXtra()
	{
		A_Explode(64,64,0);
		for(int i=0;i<3;i++)
			A_SpawnItemEx("GargFXtra",0,0,0,random(5,-5),random(5,-5),random(1,6));
	}
}

class GargFloorBoom : GargFloorBoomFake			//stays still a little
	{Default{Speed 10;}}

class GargFloorBoomSeeker : GargFloorBoom
{
	Default
	{
		+SeekerMissile
	}
	States
	{
	Spawn:
		X036 BCDEFGH 1 BRIGHT NODELAY LIGHT("RedMediumPoint2") A_GargFXtra;
		X036 I 1 BRIGHT LIGHT("RedMediumPoint2")
		{
			A_SeekerMissile(10,10);
			A_GargFXtra();
		}
		X036 JKLMNOP 1 BRIGHT LIGHT("RedMediumPoint2") A_GargFXtra;
		X036 Q 1 BRIGHT LIGHT("RedMediumPoint2")
		{
			A_SeekerMissile(10,10);
			A_GargFXtra();
		}
		X036 RSTUVWXY 1 BRIGHT LIGHT("RedMediumPoint2") A_GargFXtra;
		TNT1 A 0 A_SeekerMissile(10,10);
		Loop;
	}
}

class GargFXtra : Actor
{
	Default
	{
		Projectile;
		+NOCLIP
		Radius 1;
		+DONTBLAST
		Height 1;
		Scale .15;
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		STAR P 3 BRIGHT A_Jump(128,1);
		Loop;
		STAR P 1 Bright A_FadeOut;
		Wait;
	}
}

//******************************
// Gargantua controller fireball
//******************************
class GargControllerFireball1 : ControllerFireball1
	{Default{Speed 20;}}

//*******************************
// Gargantua cont seeker fireball
//*******************************
class GargControllerFireball2 : ControllerFireball2
{
	Default
	{
		Speed 12;
		Damage 15;
	}
}