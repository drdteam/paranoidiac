//==============
// Hornet Grunt
//==============

class HLHornetGrunt : HLMonster
{
	override void HLModelSetup()
	{
		static const name modelskin[]=
		{
			'agrunt_000.png',
			'agrunt_001.png',
			'agrunt_002.png',
			'agrunt_003.png',
			'agrunt_004.png',
			'agrunt_005.png',
			'agrunt_006.png',
			'agrunt_005.png',
			'agrunt_007.png',
			'agrunt_000.png'
		};
		for(int i = 0; i <= 9; i++)
			A_ChangeModel("", 0, "Models", "agrunt.iqm", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
		SetModelFlag(MDL_INTERPOLATEDOUBLEDFRAMES);
	}
	Default
	{
		Health 500;
		Mass 800;
		Speed 2.5;
		Height 64;
		PainChance 40;
		PainChance "Crowbar",164;
		BloodColor "DE FE 72";
		DamageFactor "Hornet",0;
		SeeSound "halflife/monsters/hornetgrunt/sight";
		PainSound "halflife/monsters/hornetgrunt/pain";
		DeathSound "halflife/monsters/hornetgrunt/death";
		ActiveSound "halflife/monsters/hornetgrunt/idle";
		Species "Xen";
		+DECOUPLEDANIMATIONS
	}
	States
	{
	Spawn:
		HLI1 A 1 NODELAY
		{
			SetAnimation('idle1', 15, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_Look();
		}
		Loop;
	See:
		HLI1 A 1
		{
			SetAnimation('walk', 15, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_HLChase();
		}
		Loop;
	Melee:
		HLI1 A 2
		{
			SetAnimation('mattack2', 15);
			A_FaceTarget();
		}
		HLI1 AAAAAAA 2 A_FaceTarget;
		HLI1 I 16
		{
			A_StartSound("halflife/monsters/zombie/attswing");
			A_CustomMeleeAttack(12*random(1,8),"halflife/monsters/zombie/hit");
		}
		Goto See;
	Missile:
		HLI1 A 2
		{
			SetAnimation('hornetattack', 10);
			A_FaceTarget();
		}
		HLI1 AAAA 2 A_FaceTarget;
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 4 A_FaceTarget;
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 4 A_FaceTarget;
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 4 A_FaceTarget;
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 4 A_FaceTarget;
		HLI1 A 0 A_Jump(80,"See");
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 0 A_Jump(100,"See");
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 0 A_Jump(120,"See");
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 0 A_Jump(140,"See");
		HLI1 A 4 A_SpawnProjectile("HGruntHornet", 36, 12);
		HLI1 A 4 A_FaceTarget;
		Goto See;
	Pain:
		HLA1 A 23
		{
			A_Pain();
			SetAnimation('smallflinch', 15);
		}
		Goto See;
	Death:
		HLD1 A 20
		{
			A_ScreamAndUnblock();
			SetAnimation('diesimple', 10, endframe:9);
		}
		HLI1 A -1 A_StartSound("halflife/weapons/bodydrop");
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(20,"XenGibs");
		Stop;
	}
}

class HLHornetGruntDead : HLHornetGrunt
{
	override void HLModelSetup()
	{
		static const name modelskin1[]=
		{
			'agrunt_000.png',
			'agrunt_001.png',
			'agrunt_002.png',
			'agrunt_003.png',
			'agrunt_004.png',
			'agrunt_005.png',
			'agrunt_006.png',
			'agrunt_000.png'
		};
		for(int i = 0; i <= 7; i++)
			A_ChangeModel("", 0, "Models", "agrunt2.iqm", i, "Models", modelskin1[i], CMDL_USESURFACESKIN);
	}
	Default
	{
		-SHOOTABLE
		-COUNTKILL
		-SOLID
		-ISMONSTER
	}
	States
	{
	Spawn:
		TNT1 A 0;
		HLA1 A -1 SetAnimation('diesimple', 10, 9, endframe:9);
		Stop;
	}
}