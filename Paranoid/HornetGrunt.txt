//==============
// Hornet Grunt
//==============

class HLHornetGrunt : HLMonster
{
	override void HLModelSetup()
	{
		static const name modelskin1[]=
		{
			'agrunt_body_000.png',
			'agrunt_body_001.png',
			'agrunt_body_002.png'
		};
		static const name modelskin2[]=
		{
			'agrunt_body_000.png',
			'agrunt_body_001.png',
			'agrunt_gun_002.png',
			'v_hgun_002.png'
		};

		for(int i = 0; i <= 2; i++)
			A_ChangeModel("", 0, "Models", "agrunt_body.iqm", i, "Models", modelskin1[i], CMDL_USESURFACESKIN);
		for(int i = 0; i <= 3; i++)
			A_ChangeModel("", 1, "Models", "agrunt_gun.iqm", i, "Models", modelskin2[i], CMDL_USESURFACESKIN, 0);
	}
	Default
	{
		Health 500;
		Mass 800;
		Speed 2.5;
		Height 64;
		PainChance 40;
		PainChance "Crowbar",164;
		BloodColor "DE FE 72";
		DamageFactor "Hornet",0;
		SeeSound "halflife/monsters/hornetgrunt/sight";
		PainSound "halflife/monsters/hornetgrunt/pain";
		DeathSound "halflife/monsters/hornetgrunt/death";
		ActiveSound "halflife/monsters/hornetgrunt/idle";
		Species "Xen";
	}
	States
	{
	See:
		HLW1 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTT 1 A_HLChase;
		Loop;
	Melee:
		HLA2 ABCDEFGH 2 A_FaceTarget;
		HLA2 I 2
		{
			A_PlaySound("halflife/monsters/zombie/attswing");
			A_CustomMeleeAttack(12*random(1,8),"halflife/monsters/zombie/hit");
		}
		HLA2 JKLMNOP 2;
		Goto See;
	Missile:
		HLA1 ABCDE 2 A_FaceTarget;
		HLA1 F 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 G 4 A_FaceTarget;
		HLA1 H 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 I 4 A_FaceTarget;
		HLA1 J 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 K 4 A_FaceTarget;
		HLA1 L 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 M 4 A_FaceTarget;
		HLA1 L 0 A_Jump(80,"See");
		HLA1 L 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 J 0 A_Jump(100,"See");
		HLA1 J 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 H 0 A_Jump(120,"See");
		HLA1 H 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 F 0 A_Jump(140,"See");
		HLA1 F 4 A_SpawnProjectile("HGruntHornet",36,8);
		HLA1 E 4 A_FaceTarget;
		Goto See;
	Pain:
		HLP1 A 1 A_Pain;
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 2 A_Scream;
		HLD1 B 2 A_NoBlocking;
		HLD1 CDE 3;
		HLD1 F 3 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 GHI 3;
		HLD1 J -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(20,"XenGibs");
		Stop;
	}
}