class OMGOrangeSpark : SwitchableDecoration
{
	Default
	{
		height 2;
		radius 1;
		+NOCLIP
		+NOSECTOR
		+NOGRAVITY
		+NOBLOCKMAP
		+LOOKALLAROUND
	}
	States
	{
	spawn:
	Active:
		TNT1 A 0 NODELAY A_PlaySound("World/Spark");
		TNT1 A 0 A_CheckSightOrRange(2560,"Inactive");
		TNT1 A 1 A_SpawnItemEx("SparkFlare_O1");
		TNT1 A 0
		{
			actor thisspark;
			for(float i=1.0;i<=16.0;i+=1.0)
			{
				for(int j=0;j<2;j++)
				{
					switch(args[0])
					{
					case 0:
						thisspark=A_SpawnProjectile("Spark_O1",0,0,random(0,359),2,Random(-67,-113));
						break;
					case 1:
						thisspark=A_SpawnProjectile("Spark_O1",0,0,random(0,359),2,Random(67,113));
						break;
					default:
						thisspark=A_SpawnProjectile("Spark_O1",0,0,Random(-23,23),2,Random(157,203));
						break;
					}
					thisspark.vel.x=(i/4.0);
				}
			}
		}
	Inactive:
		TNT1 A -1;
		Loop;
	}
}

//Flares
class SparkFlare_O1 : Actor
{
	Default
	{
		height 0;
		radius 0;
		mass 0;
		+MISSILE
		+NOBLOCKMAP
		+NOCLIP
		+NOGRAVITY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		scale .25;
	}
	states
	{
	Spawn:
		SPKO A 0 NODELAY {self.frame=(random(0,5));}
		SPKO # 1 Bright A_FadeOut;
		Wait;
	}
}

//Sparks
class Spark_O1 : SparkFlare_O1
{
	Default
	{
		height 1;
		radius 2;
		speed .25;
		-NOCLIP
		-NOGRAVITY
		-NOINTERACTION
		gravity .125;
		scale .025;
	}
	States
	{
	Spawn:
		SPKO E -1 bright;
		Loop;
	Death:
		Goto Super::Spawn+1;
	}
}

//****
//NJ Spark replacements
//Thanks to Gez, LoganMTM, solarsnowfall and Randy Heit
//****

//Briefly showers sparks downwards
 class NJDownSpark : SwitchableDecoration replaces Spark
 {
	Default
	{
		Height 2;
		Radius 2;
		Scale .05;
		RenderStyle "Add";
		Alpha .8;
		+NOBLOCKMAP
		+NOGRAVITY
		+DONTSPLASH
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		{
			alpha=.8;
			if(!args[0])
				A_SetArg(0,32);
		}
	Inactive:
		TNT1 A -1;
		Wait;
	Active:
		SPKO D 1 Bright LIGHT("NJSPARKY") {alpha=.8;}
		SPKO D 1 Bright LIGHT("NJSPARKY") A_PlaySound("world/spark",CHAN_AUTO,1.0,0,2);
		SPKO D 0
		{
			for(int i=0;i<args[0];i++)
				A_SpawnItemEx("SparkyBall",Frandom(-1,1),Frandom(-1,1),Frandom(-1,1),Frandom(0,1),Frandom(0,1),Frandom(-5,1),Random(-90,90));
		}
		SPKO DDD 2 LIGHT("NJSPARKY") A_FadeOut(.2);
		SPKO D 0 Thing_Deactivate(0);
		Goto Spawn;
	}
}

 //sparks spawned by the downward sparker
class SparkyBall : Actor
{
	Default
	{
		Height 2;
		Radius 2;
		Speed 3;
		Scale .01;
		RenderStyle "Add";
		Alpha .8;
		gravity .125;
		+DONTSPLASH
		+DROPOFF
		+NOTELEPORT
		+NOBLOCKMAP
		+FORCEXYBILLBOARD
		+NOCLIP
		+CORPSE
	}
	States
	{
	Spawn:
		SPKO BEAC 1 BRIGHT;
		SPKO B 0 A_Jump(75,"Crash");
		Loop;
	Crash:
		SPKO BEAC 1 BRIGHT A_FadeOut;
		Loop;
	}
}

//****
//Gargantua Big Blue Spark
//****
class BigBlueSpark : SwitchableDecoration
{
	int iterator;
	Default
	{
		height 2;
		radius 1;
		+NOCLIP
		+NOSECTOR
		+NOGRAVITY
		+NOBLOCKMAP
		+LOOKALLAROUND
	}
	States
	{
	spawn:
		TNT1 A 1;
	Inactive:
		TNT1 A -1 {iterator=0;}
		Loop;
	Active:
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1");
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.25,2);
			A_Spark("Spark_BB1",4,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.5,2);
			A_Spark("Spark_BB1",3.75,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",1);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.75,2);
			A_Spark("Spark_BB1",3.5,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1,2);
			A_Spark("Spark_BB1",3.25,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",2);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.25,2);
			A_Spark("Spark_BB1",3,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.5,2);
			A_Spark("Spark_BB1",2.75,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",3);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.75);
			A_Spark("Spark_BB1",2.5);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",2);
			A_Spark("Spark_BB1",2.25);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",4);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",2.25);
			A_Spark("Spark_BB1",2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",2.5);
			A_Spark("Spark_BB1",1.75);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",5);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",2.75);
			A_Spark("Spark_BB1",1.5);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",3);
			A_Spark("Spark_BB1",1.25);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",1);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",3.25);
			A_Spark("Spark_BB1",1);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",3.5);
			A_Spark("Spark_BB1",.75);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",3);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",3.75);
			A_Spark("Spark_BB1",.5);
		}
		TNT1 A 0 {iterator++;}
		TNT1 A 0 A_JumpIf(iterator>=4,1);
		Loop;
		//and again - shorter this time
		TNT1 A 1
		{
			A_Spark("Spark_BB1",4);
			A_Spark("Spark_BB1",.25);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1");
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.25,2);
			A_Spark("Spark_BB1",4,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.5,2);
			A_Spark("Spark_BB1",3.75,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",1);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",.75,2);
			A_Spark("Spark_BB1",3.5,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1,2);
			A_Spark("Spark_BB1",3.25,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",2);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.25,2);
			A_Spark("Spark_BB1",3,2);
		}
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.5,2);
			A_Spark("Spark_BB1",2.75,2);
		}
		TNT1 A 1 A_SparkFlare("SparkFlare_BB1",3);
		TNT1 A 1
		{
			A_Spark("Spark_BB1",1.75);
			A_Spark("Spark_BB1",2.5);
		}
		Goto Inactive;
	}
	void A_Spark(class<actor> spark,float spd,int num=1)
	{
		actor thisspark;
		for(int i=0;i<num;i++)
		{
			thisspark=A_SpawnProjectile(spark,0,0,random(0,359),2,Random(-255,255));
			thisspark.vel.x=spd;
		}
	}
	void A_SparkFlare(class<actor> flare,int frm=0)
	{
		bool spawned;
		actor thisflare;
		[spawned,thisflare]=A_SpawnItemEx(flare);
		thisflare.frame=frm;
	}
}

//Big Blue Spark Flares
class SparkFlare_BB1 : SparkFlare_O1
{
	Default
	{
		scale 1;
		Alpha 1.1;
	}
	states
	{
	Spawn:
		SPKB A 0;
		SPKB # 1 Bright LIGHT("BIGBLUESPARKLIGHT") A_FadeOut;
		Wait;
	}
}

//Big Blue Sparks
class Spark_BB1 : Spark_O1
{
	Default
	{
		+NOCLIP
		+CORPSE
		scale .05;
	}
	states
	{
	Spawn:
		SPKB E -1 Bright;
		Loop;
	Crash:
		SPKB E 1 Bright A_FadeOut;
		Loop;
	Death:
		Stop;
	}
}

//Flares in Vacuum Tubes at the end of Map01
class NJTankSpark : BlueTorch replaces BlueTorch
{
	Default
	{
		scale .4;
		RenderStyle "Add";
		Alpha .9;
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		TBLU ABCD 2 BRIGHT LIGHT("TANKSPARKEFFECT");
		Loop;
	}
}