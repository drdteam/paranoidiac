class HLZombie : HLMonster
{
	override void HLModelSetup()
	{
		static const name modelskin1[]=
		{
			'zombie1_000.png',
			'zombie1_001.png',
			'zombie1_002.png',
			'zombie1_003.png',
			'zombie1_004.png',
			'zombie1_005.png',
			'zombie1_006.png'
		};
		static const name modelskin2[]=
		{
			'zombie1_002.png',
			'zombie2_001.png',
			'zombie2_002.png',
			'zombie2_003.png',
			'zombie2_004.png',
			'zombie2_005.png',
			'headcrab_001.png'
		};
		static const name modelskin3[]=
		{
			'zombie1_002.png',
			'zombie2_001.png',
			'zombie2_002.png',
			'zombie2_003.png',
			'zombie3_004.png',
			'headcrab_001.png',
			'zombie3_006.png',
			'zombie3_007.png'
		};
		switch(random(1, 3))
		{
		case 1:
			for(int i = 0; i <= 6; i++)
				A_ChangeModel("", 0, "Models", "zombie1.iqm", i, "Models", modelskin1[i], CMDL_USESURFACESKIN);
			break;
		case 2:
			for(int i = 0; i <= 6; i++)
				A_ChangeModel("", 0, "Models", "zombie2.iqm", i, "Models", modelskin2[i], CMDL_USESURFACESKIN);
			break;
		case 3:
			for(int i = 0; i <= 7; i++)
				A_ChangeModel("", 0, "Models", "zombie3.iqm", i, "Models", modelskin3[i], CMDL_USESURFACESKIN);
			break;
		}
	}
	Default
	{
		Health 150;
		Mass 100;
		Speed 1.4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/zombie/sight";
		PainSOund "halflife/monsters/zombie/pain";
		DeathSound "halflife/monsters/zombie/pain";
		ActiveSound "halflife/monsters/zombie/idle";
		maxdropoffheight 64;
		Species "Xen";
		HLMonster.WalkTic 5;
		+DECOUPLEDANIMATIONS
	}
	States
	{
	Spawn:
		HLI1 A 2
		{
			SetAnimation('idle1', 15, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_Look();
		}
		Loop;
	See:
		HLI1 A 1
		{
			SetAnimation('walk', 22, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_HLChase();
		}
		Loop;
	Melee:
		HLA1 A 6
		{
			SetAnimation('attack1', 35);
			A_FaceTarget();
		}
		HLA1 A 3 A_ZombieMelee;
		HLA1 A 6 A_FaceTarget;
		HLA1 A 3 A_ZombieMelee;
		HLA1 A 11 A_FaceTarget;
		Goto See;
	Pain:
		HLI1 A 8
		{
			A_Pain();
			SetAnimation('flinch', 22);
		}
		Goto See;
	Death:
		HLI1 A 40
		{
			A_ScreamAndUnblock();
			SetAnimation('dieforward', 18, endframe:29);
		}
		HLI1 A -1 A_StartSound("halflife/weapons/bodydrop");
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(16,"XenGibs");
		Stop;
	}
}