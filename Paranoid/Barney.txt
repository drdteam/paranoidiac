//=========
// Barney
//=========

class HLBarneyGuardSpawner : RandomSpawner
{
	Default
	{
		+FRIENDLY
		DropItem "HLBarney";
	}
	override void PostSpawn(Actor spawned)
	{
		let me=HLBarney(spawned);
		me.guard=1;
		me.SetStateLabel("guard");
		me.bUSESPECIAL=1;
	}
}
class HLOtisGuardSpawner : HLBarneyGuardSpawner
{
	Default
	{
		DropItem "HLOtis";
	}
}

class HLBarney : HLMonster
{
	bool follow,follower,guard;
	override void tick()
	{
		if(follower)
		{
			if(goal==null)
				goal=players[0].mo;
			if(!follow&&Distance3D(goal)>96.0)
			{
				follow=1;
				SetStateLabel("See");
			}
			else if(follow&&Distance3D(goal)<=96.0)
			{
				follow=0;
				A_ClearTarget();
				if(random(0,1))
					SetStateLabel("Stand");
				else
					SetStateLabel("Stand2");
			}
		}
		Super.Tick();
	}
	Default
	{
		Health 150;
		Speed 4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		SeeSound "halflife/monsters/barney/idle";
		PainSound "halflife/monsters/barney/pain";
		DeathSound "halflife/monsters/barney/death";
		MaxDropoffHeight 64;
		DropItem "HLPistolPickup";
		+FRIENDLY
		-COUNTKILL
		Species "HLBarney";
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_Look;
		HLI2 AB 2 A_Look;
		Loop;
	Stand:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_Face(goal);
		HLI2 AB 2 A_Face(goal);
		Loop;
	Stand2:
		HLI2 CDEFGHIJKLMNOPQRSTUVWXYZ[\] 2 A_Face(goal);
		HLI3 ABCD 2 A_Face(goal);
		Loop;
	Guard:
		HLI3 EFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI4 ABCDEF 2;
		Loop;
	See:
		HLR1 A 1 A_Chase;
		HLR1 BCD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 E 1 A_Chase;
		HLR1 FGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 I 1 A_Chase;
		HLR1 JKL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 M 1 A_Chase;
		HLR1 NOP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 Q 1 A_Chase;
		HLR1 RST 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 U 1 A_Chase;
		HLR1 VW 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLR1 A 0 A_Chatter;
		Loop;
	Missile:
		HLA1 A 1 A_FaceTarget;
		HLA1 BCD 1;
		HLA1 E 1 A_FaceTarget;
		HLA1 FGH 1;
		HLA1 I 1 A_FaceTarget;
		HLA1 JKL 1;
		HLA1 M 1 A_FaceTarget;
		HLA1 NOP 1;
		HLA1 Q 1 A_FaceTarget;
		HLA1 RST 1;
		HLA1 U 1 A_FaceTarget;
		HLA1 V 1;
		TNT1 A 0 A_Jump(64,"Magnum");
	Pistol:
		HLA1 W 1 A_FaceTarget;
		HLA1 X 1;
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot;
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 VV 2 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(0,"See");
		Loop;
	Magnum:
		HLA1 W 1 A_FaceTarget;
		HLA1 X 1;
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot(2);
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 V 6 A_FaceTarget;
		HLA1 VVVV 8 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(0,"See");
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot(2);
		HLA1 Z[\] 1 Light("ImShootingAGun");
		HLA2 AB 1;
		HLA1 V 6 A_FaceTarget;
		HLA1 VVVV 8 A_FaceTarget;
		Goto See;
	Pain:
		HLP1 A 1 A_Pain;
		HLP1 DGJMPSU 1;
		HLP1 U 0 A_JumpIf(guard,"Guard");
		Goto See;
	Death:
		HLD1 A 2 A_ScreamAndUnblock;
		HLD1 BCDEFG 2;
		HLD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 IJKL 2;
		HLD1 M -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs;
		Stop;
	}
	void A_BarneyGunshot(int type=1)
	{
		A_BarneyGunsmoke();
		if(type==2)
		{
			A_PlaySound("halflife/weapons/magnumshot",CHAN_WEAPON);
			A_CustomBulletAttack(2,2,1,25,"BulletPuff");
		}
		else
		{
			A_PlaySound("halflife/weapons/pistol",CHAN_WEAPON);
			A_CustomBulletAttack(7,7,1,5,"BulletPuff");
		}
	}
	void A_BarneyGunsmoke()
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx("GunSmoke",28,2,48);
	}
	virtual void A_Chatter()
	{
		if(!random(0,32))
			A_PlaySound("halflife/monsters/barney/idle");
	}
}

class HLOtis : HLBarney
{
	Default
	{
		SeeSound "halflife/monsters/otis/idle";
		PainSound "halflife/monsters/otis/pain";
		DeathSound "halflife/monsters/otis/death";
	}
	override void A_Chatter()
	{
		if(!random(0,32))
			A_PlaySound("halflife/monsters/otis/idle");
	}
}