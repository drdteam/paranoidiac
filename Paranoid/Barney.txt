//=========
// Barney
//=========

class HLBarneyGuardSpawner : RandomSpawner
{
	Default
	{
		+FRIENDLY
		DropItem "HLBarney";
	}
	override void PostSpawn(Actor spawned)
	{
		let me=HLBarney(spawned);
		me.guard=1;
		me.SetStateLabel("guard");
		me.bUSESPECIAL=1;
	}
}
class HLOtisGuardSpawner : HLBarneyGuardSpawner
	{Default{DropItem "HLOtis";}}
class TramBarney : HLBarneyGuardSpawner
{
	override void PostSpawn(Actor spawned)
	{
		spawned.SetStateLabel("Tram");
		spawned.bNOCLIP=1;
	}
}

class HLBarney : HLMonster
{
	bool follower,guard;
	actor bestfriend;
	Default
	{
		Health 150;
		Speed 4;
		Height 63;
		PainChance 128;
		PainChance "Crowbar",252;
		SeeSound "halflife/monsters/barney/idle";
		PainSound "halflife/monsters/barney/pain";
		DeathSound "halflife/monsters/barney/death";
		MaxDropoffHeight 64;
		DropItem "HLPistol";
		+AMBUSH
		+FRIENDLY
		-COUNTKILL
		Species "BlackMesa";
	}
	States
	{
	Spawn:
		HLI1 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTTUUVVWWXXYYZZ[[\\]] 1 A_HLBarneyLook;
		HLI2 AABB 1 A_HLBarneyLook;
		Loop;
	Spawn2:
		HLI2 CCDDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTTUUVVWWXXYYZZ[[\\]] 1 A_HLBarneyLook;
		HLI3 AABBCCDD 1 A_HLBarneyLook;
		Loop;
	Guard:
		HLI3 EFGHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI4 ABCDEF 2;
		Loop;
	Tram:
		HLI4 GHIJKLMNOPQRSTUVWXYZ[\] 2;
		HLI5 ABCDEFGH 2;
		Loop;
	See:
		HLR1 ABCDEFGHIJKLMNOPQRSTUVW 1 A_HLBarneyChase;
		HLR1 A 0 A_Chatter;
		Loop;
	Missile:
		HLA1 ABCDEFGHIJKLMNOPQRSTUV 1 A_FaceTarget;
	Pistol:
		HLA1 WX 1 A_FaceTarget;
		HLA1 Y 1 Light("ImShootingAGun") A_BarneyGunshot;
		HLA1 Z[\] 1 Light("ImShootingAGun") A_FaceTarget;
		HLA2 AB 1 A_FaceTarget;
		HLA1 VVVV 1 A_FaceTarget;
		TNT1 A 0 A_MonsterRefire(0,"See");
		Loop;
	Pain:
		HLP1 A 1 A_Pain;
		HLP1 DGJMPSU 1;
		HLP1 U 0 A_JumpIf(guard,"Guard");
		Goto See;
	Death:
		HLD1 A 2 A_ScreamAndUnblock;
		HLD1 BCDEFG 2;
		HLD1 H 2 A_PlaySound("halflife/weapons/bodydrop");
		HLD1 IJKL 2;
		HLD1 M -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs;
		Stop;
	}
	virtual void A_BarneyGunshot()
	{
		A_FaceTarget();
		A_BarneyGunsmoke();
		A_PlaySound("halflife/weapons/pistol",CHAN_WEAPON);
		A_CustomBulletAttack(7,7,1,5,"NewPuff1");
	}
	void A_BarneyGunsmoke()
	{
		for(int i=0;i<2;i++)
			A_SpawnItemEx("GunSmoke",28,2,47);
	}
	virtual void A_Chatter()
	{
		if(!guard&&!random(0,32))
			A_PlaySound("halflife/monsters/barney/idle");
	}
	action actor ChooseBestFriend(actor oldfriend)
	{
		if(oldfriend&&oldfriend.health>0)
			return oldfriend;
		Actor mo;
		PlayerInfo player;
		double dist;
		for(int i=0;i<MAXPLAYERS;i++)
		{
			if(!playeringame[i])
				continue;
			player=players[i];
			mo=player.mo;
			if(mo.health<=0||Distance3D(mo)>256.0)
				continue;
			return mo;
		}
		return null;
	}
	action void A_HLBarneyLook()
	{
		if(goal)
		{
			A_Face(goal);
			return;
		}
		let self=HLBarney(self);
		if(!self.follower)
		{
			A_Look();
			return;
		}
		self.bestfriend=ChooseBestFriend(self.bestfriend);
		if(!self.bestfriend)
		{
			A_Look();
			return;
		}
		if(target||Distance3D(self.bestfriend)>128.0)
			SetStateLabel("See");
		else
		{
			A_Face(self.bestfriend);
			A_LookEx(LOF_DONTCHASEGOAL|LOF_NOSEESOUND|LOF_NOJUMP,0,0,360);
		}
	}
	action void A_HLBarneyChase()
	{
		if(goal)
		{
			self.A_HLChase();
			return;
		}
		let self=HLBarney(self);
		if(!self.follower||self.target)
		{
			self.A_HLChase();
			return;
		}
		self.bestfriend=ChooseBestFriend(self.bestfriend);
		if(!self.bestfriend)
		{
			self.A_HLChase();
			return;
		}
		if(Distance3D(self.bestfriend)>96.0)
		{
			self.goal=self.bestfriend;
			self.A_HLChase();
			return;
		}
		if(random(0,1))
			SetStateLabel("Spawn");
		else
			SetStateLabel("Spawn2");
	}
}

class HLOtis : HLBarney
{
	Default
	{
		SeeSound "halflife/monsters/otis/idle";
		PainSound "halflife/monsters/otis/pain";
		DeathSound "halflife/monsters/otis/death";
		DropItem "HLDesertEagle";
	}
	override void A_Chatter()
	{
		if(!guard&&!random(0,32))
			A_PlaySound("halflife/monsters/otis/idle");
	}
	override void A_BarneyGunshot()
	{
		A_FaceTarget();
		A_BarneyGunsmoke();
		A_PlaySound("halflife/weapons/deagleshot",CHAN_WEAPON);
		A_CustomBulletAttack(1,1,1,18,"NewPuff1");
	}
}