//==================
// Charging Stations
//==================

class FirstAidStation : Actor
{
	bool inuse;
	override bool Used(Actor user)
	{
		user.A_StopSound(CHAN_VOICE);
		if(inuse)
		{
			inuse=0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
		if(user.Health>=100||CheckRange(radius+30,0))
			return false;
		if(!Health)
		{
			A_PlaySound("MEDCHARGEUSED");
			return false;
		}
		inuse=1;
		target=user;
		SetStateLabel("Working");
		return false;
	}
	Default
	{
		+SOLID
		+NOGRAVITY
		Height 48;
		Radius 16;
		Health 40;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Working:
		HLI1 A 5 A_HLChargingStation;
		Loop;
	Death:
		HLI1 B -1;
		Stop;
	}
	virtual void A_HLChargingStation()
	{
		A_PlaySound("MEDCHARGE",looping:1);
		target.GiveInventory("Health",1);
		Health--;
		if(!Health)
		{
			inuse=0;
			A_PlaySound("MEDCHARGEUSED");
			SetStateLabel("Death");
		}
		if(target.Health>=100||CheckRange(radius+30,0))
		{
			inuse=0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
	}
}

class HEVChargingStation : FirstAidStation
{
	override bool Used(Actor user)
	{
		user.A_StopSound(CHAN_VOICE);
		if(inuse)
		{
			inuse=0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
		if(user.CountInv("BasicArmor")>=100||CheckRange(radius+30,0))
			return false;
		if(!Health)
		{
			A_PlaySound("HEVCHARGEUSED");
			return false;
		}
		inuse=1;
		target=user;
		SetStateLabel("Working");
		return false;
	}
	Default
	{
		Health 50;
	}
	override void A_HLChargingStation()
	{
		A_PlaySound("HEVCHARGE",looping:1);
		target.GiveInventory("HEVCharge",1);
		Health--;
		if(!Health)
		{
			inuse=0;
			A_PlaySound("HEVCHARGEUSED");
			SetStateLabel("Death");
		}
		if(target.CountInv("BasicArmor")>=100||CheckRange(radius+30,0))
		{
			inuse=0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
	}
}
class HEVCharge : BasicArmorBonus
{
	Default
	{
		Armor.SaveAmount 1;
		Armor.SavePercent 50;
		Armor.MaxSaveAmount 100;
		-INVENTORY.ALWAYSPICKUP
		Inventory.Icon "i_arm2";
	}
}

//=========
// Pickups
//=========

class HEVSuit : BasicArmorPickup
{
	Default
	{
		Radius 16;
		Height 52;
		Inventory.PickupMessage "Acquired the HEV Suit!";
		Armor.SaveAmount 100;
		Armor.SavePercent 50;
		Inventory.Icon "i_arm2";
	}
	states
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class HEVbattery : HEVCharge
{
	Default
	{
		Radius 4;
		Height 12;
		Inventory.PickupMessage "Picked up a battery pack for the HEV Suit.";
		Armor.SaveAmount 15;
	}
	States
	{
	Spawn:
		HLI1 A -1 LIGHT("HevBattLight");
		Stop;
	}
}

class Medkit : Health
{
	Default
	{
		Radius 8;
		Height 4;
		Inventory.PickupMessage "Picked up a field medical kit.";
		Inventory.Amount 15;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class Medkit1 : HealthPickup
{
	Default
	{
		Tag "Portable Medkit";
		+COUNTITEM
		+INVENTORY.HUBPOWER
		+INVENTORY.INVBAR
		Health 25;
		Inventory.MaxAmount 4;
		Inventory.Icon "MEDKZ0";
		Inventory.PickupMessage "Picked up a portable medkit.";
		Inventory.PickupSound "misc/i_pkup";
		Inventory.UseSound "misc/i_pkup";
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class Key1 : FakeInventory
{
	Default
	{
		Scale .5;
		Radius 4;
		Height 2;
		Inventory.PickupMessage "Picked up a security key.";
	}
	States
	{
	Spawn:
		HLI1 A -1 NODELAY {frame=random(0,1);}
		Stop;
	}
}

class Gascan1 : Key1
{
	Default
	{
		Height 12;
		Scale .75;
		Mass 20;
		Inventory.PickupMessage "Acquired gasoline can.";
	}
}

class Explosive : Gascan1
{
	Default
	{
		Height 8;
		Mass 10;
		Inventory.PickupMessage "Acquired explosive device.";
	}
}