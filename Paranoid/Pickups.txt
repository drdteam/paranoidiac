//==================
// Charging Stations
//==================

class FirstAidStation : ModelBase
{
	override void HLModelSetup()
	{
		static const name modelskin1[] =
		{
			'medcharger_000.png',
			'medcharger_001.png',
			'medcharger_002.png',
			'medcharger_003.png'
		};
		static const name modelskin2[] =
		{
			'medchargerb_000.png',
			'medchargerb_001.png'
		};
		for(int i = 0; i <= 3; i++)
			A_ChangeModel("ModelBase", 0, "Models", "medcharger.md3", i, "Models", modelskin1[i], CMDL_USESURFACESKIN);
		for(int i = 0; i <= 1; i++)
			A_ChangeModel("ModelBase", 1, "Models", "medchargerb.md3", i, "Models", modelskin2[i], CMDL_USESURFACESKIN);
	}
	bool inuse;
	override bool Used(Actor user)
	{
		user.A_StopSound(CHAN_VOICE);
		if(inuse)
		{
			inuse = 0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
			return false;
		}
		if(user.Health >= 100 || CheckRange(radius + 30, 0))
			return false;
		if(!Health)
		{
			A_StartSound("MEDCHARGEUSED");
			SetStateLabel("Death");
			return false;
		}
		inuse = 1;
		target = user;
		SetStateLabel("Working");
		return false;
	}
	Default
	{
		+SOLID
		+NOGRAVITY
		Height 48;
		Radius 16;
		Health 40;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	Working:
		HLI1 A 5 A_HLChargingStation;
		Loop;
	Death:
		HLI1 A -1 A_ChangeModel("ModelBase", 0, "Models", "medcharger.md3", 1, "Models", 'medcharger_004.png', CMDL_USESURFACESKIN);
		Stop;
	}
	virtual void A_HLChargingStation()
	{
		A_StartSound("MEDCHARGE", flags: CHANF_LOOPING);
		target.GiveInventory("Health", 1);
		Health--;
		if(!Health)
		{
			inuse = 0;
			A_StartSound("MEDCHARGEUSED");
			SetStateLabel("Death");
		}
		if(target.Health >= 100 || CheckRange(radius + 30, 0))
		{
			inuse = 0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
	}
}

class HEVChargingStation : FirstAidStation
{
	override void HLModelSetup()
	{
		static const name modelskin[] =
		{
			'hevcharger_000.png',
			'hevcharger_001.png',
			'hevcharger_002.png',
			'hevcharger_003.png'
		};
		for(int i = 0; i <= 3; i++)
			A_ChangeModel("ModelBase", 0, "Models", "hevcharger.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
	}
	override bool Used(Actor user)
	{
		user.A_StopSound(CHAN_VOICE);
		if(inuse)
		{
			inuse = 0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
			return false;
		}
		if(user.CountInv("BasicArmor") >= 100 || CheckRange(radius + 30, 0))
			return false;
		if(!Health)
		{
			A_StartSound("HEVCHARGEUSED");
			SetStateLabel("Death");
			return false;
		}
		inuse = 1;
		target = user;
		SetStateLabel("Working");
		return false;
	}
	Default
		{Health 50;}
	override void A_HLChargingStation()
	{
		A_StartSound("HEVCHARGE", flags: CHANF_LOOPING);
		target.GiveInventory("HEVCharge", 1);
		Health--;
		if(!Health)
		{
			inuse = 0;
			A_StartSound("HEVCHARGEUSED");
			SetStateLabel("Death");
		}
		if(target.CountInv("BasicArmor") >= 100 || CheckRange(radius + 30, 0))
		{
			inuse = 0;
			A_StopSound(CHAN_BODY);
			SetStateLabel("Spawn");
		}
	}
	States
	{
	Death:
		HLI1 A -1 A_ChangeModel("ModelBase", 0, "Models", "hevcharger.md3", 0, "Models", 'hevcharger_004.png', CMDL_USESURFACESKIN);
		Stop;
	}
}
class HEVCharge : BasicArmorBonus
{
	Default
	{
		Armor.SaveAmount 1;
		Armor.SavePercent 50;
		Armor.MaxSaveAmount 100;
		-INVENTORY.ALWAYSPICKUP
		Inventory.Icon "i_arm2";
	}
}

//=========
// Pickups
//=========

class HEVSuit : BasicArmorPickup
{
	override void BeginPlay()
	{
		static const name modelskin[] =
		{
			'w_suit_000.png',
			'w_suit_001.png',
			'w_suit_002.png',
			'w_suit_003.png',
			'w_suit_004.png',
			'w_suit_005.png',
			'w_suit_006.png',
			'w_suit_007.png',
			'w_suit_006.png',
			'w_suit_002.png',
			'w_suit_002.png'
		};
		for(int i = 0; i <= 10; i++)
			A_ChangeModel("ModelBase", 0, "Models", "w_suit.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
		super.BeginPlay();
	}
	Override bool TryPickup(in out Actor toucher)
	{
		//Remove the Scientist disguise upon pickup
		if(super.TryPickup(toucher))
		{
			let me = RolfBellmer(toucher);
			if(me)
				me.Scientist = 0;
			return 1;
		}
		return 0;
	}
	Default
	{
		Radius 16;
		Height 52;
		Inventory.PickupSound "HEVPickup";
		Inventory.PickupMessage "Acquired the HEV Suit!";
		Armor.SaveAmount 100;
		Armor.SavePercent 50;
		Inventory.Icon "i_arm2";
	}
	states
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class HEVbattery : HEVCharge
{
	override void BeginPlay()
	{
		static const name modelskin[] =
		{
			'w_battery_000.png',
			'w_battery_001.png',
			'w_battery_002.png',
			'w_battery_003.png',
			'w_battery_004.png'
		};
		for(int i = 0; i <= 4; i++)
			A_ChangeModel("ModelBase", 0, "Models", "w_battery.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
		super.BeginPlay();
	}
	Default
	{
		Radius 4;
		Height 12;
		Inventory.PickupMessage "Picked up a battery pack for the HEV Suit.";
		Armor.SaveAmount 15;
	}
	States
	{
	Spawn:
		HLI1 A -1 LIGHT("HevBattLight");
		Stop;
	}
}

class Medkit : Health
{
	override void BeginPlay()
	{
		static const name modelskin[] =
		{
			'w_medkit_000.png',
			'w_medkit_001.png',
			'w_medkit_002.png',
			'w_medkit_003.png',
			'w_medkit_004.png'
		};
		for(int i = 0; i <= 4; i++)
			A_ChangeModel("ModelBase", 0, "Models", "w_medkit.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
		super.BeginPlay();
	}
	Default
	{
		Radius 8;
		Height 4;
		Inventory.PickupMessage "Picked up a field medical kit.";
		Inventory.Amount 15;
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class Medkit1 : HealthPickup
{
	override void BeginPlay()
	{
		static const name modelskin[] =
		{
			'w_medkit1_000.png',
			'w_medkit1_001.png',
			'w_medkit1_002.png',
			'w_medkit_003.png',
			'w_medkit1_004.png'
		};
		for(int i = 0; i <= 4; i++)
			A_ChangeModel("ModelBase", 0, "Models", "w_medkit.md3", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
		super.BeginPlay();
	}
	Default
	{
		Tag "Portable Medkit";
		+COUNTITEM
		+INVENTORY.HUBPOWER
		+INVENTORY.INVBAR
		Health 25;
		Inventory.MaxAmount 4;
		Inventory.Icon "MEDKZ0";
		Inventory.PickupMessage "Picked up a portable medkit.";
		Inventory.PickupSound "misc/i_pkup";
		Inventory.UseSound "misc/i_pkup";
	}
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class HLFakeInventory : FakeInventory
{
	States
	{
	Spawn:
		HLI1 A -1;
		Stop;
	}
}

class Key1 : HLFakeInventory
{
	override void BeginPlay()
	{
		static const name modelskin[] =
		{
			'w_security.png',
			'w_security2.png'
		};
		A_ChangeModel("ModelBase", 0, "Models", "w_security.md3", 0, "Models", modelskin[random(0, 1)]);
		super.BeginPlay();
	}
	Default
	{
		Scale .5;
		Radius 4;
		Height 2;
		Inventory.PickupMessage "Picked up a security key.";
	}
}

class Gascan1 : HLFakeInventory
{
	override void BeginPlay()
	{
		A_ChangeModel("ModelBase", 0, "Models", "gascan1.md3", 0, "Models", "gascan1.jpg");
		super.BeginPlay();
	}
	Default
	{
		Radius 4;
		Height 12;
		Scale .75;
		Mass 20;
		Inventory.PickupMessage "Acquired gasoline can.";
	}
}

class Explosive : HLFakeInventory
{
	override void BeginPlay()
	{
		A_ChangeModel("ModelBase", 0, "Models", "explosive.md3", 0, "Models", "explosive.jpg");
		super.BeginPlay();
	}
	Default
	{
		Radius 4;
		Scale .75;
		Height 8;
		Mass 10;
		Inventory.PickupMessage "Acquired explosive device.";
	}
}