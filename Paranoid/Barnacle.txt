//==============================================================
//Credit to CaptainToenail for the original hanger monster idea
//==============================================================

class HLBarnacle : HLMonster
{
	override void HLModelSetup()
		{A_ChangeModel("", 0, "Models", "barnacle.iqm", 0, "Models", "barnacle.png");}
	Default
	{
		obituary "%o was spat on by a Barnacle.";
		health 80;
		height 32;
		+DONTTHRUST
		maxtargetrange 384;
		painchance 128;
		PainChance "Crowbar",252;
		BloodColor "DE FE 72";
		seesound "barnacle/sight";
		painsound "barnacle/pain";
		deathsound "barnacle/death";
		activesound "barnacle/active";
		+FLOORCLIP
		+NOGRAVITY
		+SPAWNCEILING
		+CEILINGHUGGER
		+DONTFALL
		+DECOUPLEDANIMATIONS
		Species "Xen";
	}
	states
	{
	Spawn:
	See:
		HLI1 A 2 NODELAY
		{
			SetAnimation('idle3', 15, flags: SAF_LOOP|SAF_NOOVERRIDE);
			A_HLChase();
		}
		Loop;
	Missile:
		HLA1 A 9 SetAnimation('attack1', 30);
		HLA1 A 12
		{
			for(int i=0;i<7;i++)
				A_SpawnItemEx("BarnacleSpitParticle2",0,0,12,frandom(0.5,2),frandom(1.3,-1.3),frandom(-0.3,-1.2),random(0,359));
			A_StartSound("barnacle/attack");
			A_SpawnProjectile("BarnacleSpitBall",12);
		}
		HLI1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_HLChase;
		Goto See;
	Pain:
		HLI1 A 0
		{
			A_Pain();
			SetAnimation('flinch_sm', 30);
		}
		HLI1 AAAAAAAAAAAAAAAA 1 A_HLChase;
		Goto See;
	Death:
		HLI1 A 21
		{
			bNOCLIP=1;				//stops you being able to jump up and push the model into the ceiling
			A_ScreamAndUnblock();
			SetAnimation('death', 15, endframe: 99);
		}
		HLI1 AAAAAA 2 A_SpawnItemEx("HLGibs",random(3,-3),random(3,-3),random(8,24),random(1,2),random(1,2),random(-1,-3),random(0,359));
		HLI1 A -1;
		Stop;
	}
	override void A_HLChase(int walkticmax)
	{
		if(!walkticmax)
			walkticmax=walktic;
		if(!walkticcur)
		{
			if(target)
				A_Chase(null,"Missile",CHF_NODIRECTIONTURN);
			else
				A_Look();
		}
		walkticcur++;
		if(walkticcur>=walkticmax-1)
			walkticcur=0;
	}
}

class BarnacleSpitParticle : BullSquidSpitParticle
{
	Default
	{
		Alpha .67;
		Translation "80:111=160:167";
		Scale .1;
	}
}
class BarnacleSpitParticle2 : BarnacleSpitParticle
	{Default{Scale .2;}}

class BarnacleSpitBall : BullSquidSpitBall
{
	override void tick()
	{
		A_SpawnItemEx("BarnacleSpitTrail",frandom(-2.3,2.3),frandom(-2.3,2.3),frandom(-2.3,2.3));
		super.tick();
	}
	Default
	{
		Translation "80:111=160:167";
		Speed 10;
		Damage 4;
		Scale .3;
	}
	states
	{
	Death:
		TNT1 A 0 A_HLSpitParticle("BarnacleSpitParticle",8);
		Stop;
	}
}

class BarnacleSpitTrail : BullSquidSpitParticle
{
	Default
	{
		Scale 1;
		Translation "112:127=162:167";
		Alpha .8;
		+NOCLIP
		+DONTSPLASH
		Gravity .4;
	}
	States
	{
	Spawn:
		BSP2 A 1 A_FadeOut(.03);
		Wait;
	}
}