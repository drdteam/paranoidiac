//==========
// HoundEye
//==========

class HLHoundEye : HLMonster
{
	override void HLModelSetup()
	{
		static const name modelskin[]=
		{
			'houndeye_000.png',
			'houndeye_001.png'
		};
		for(int i = 0; i <= 1; i++)
			A_ChangeModel("", 0, "Models", "houndeye.iqm", i, "Models", modelskin[i], CMDL_USESURFACESKIN);
	}
	Default
	{
		Health 100;
		Mass 120;
		Speed 3.666667;
		Height 45;
		PainChance 128;
		PainChance "Crowbar",252;
		MeleeRange 80;
		BloodColor "DE FE 72";
		SeeSound "halflife/monsters/houndeye/sight";
		PainSound "halflife/monsters/houndeye/pain";
		DeathSound "halflife/monsters/houndeye/death";
		ActiveSound "halflife/monsters/houndeye/idle";
		Species "Xen";
		-DOHARMSPECIES
		HLMonster.WalkTic 3;
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1 A_Look;
		HLI2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1 A_Look;
		HLI3 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1 A_Look;
		HLI4 ABCDEFGHIJKLMN 1 A_Look;
		Loop;
	See:
		HLR1 ABCDEFGHIJKLMNOPQRS 1 A_HLChase;
		HLR1 T 0
		{
			if(random(0,255)<32)
				A_StartSound("halflife/monsters/houndeye/idlefull");
		}
		HLR1 TUVWXYZ[\] 1 A_HLChase;
		HLR2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1 A_HLChase;
		HLR2 ] 0
		{
			if(random(0,1))
				A_StartSound("halflife/monsters/houndeye/idlefull");
		}
		Loop;
	Melee:
		HLA1 A 3
		{
			A_FaceTarget();
			A_StartSound("halflife/monsters/houndeye/attackcharge", CHAN_5);
		}
		HLA1 BCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLA2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 1;
		HLA1 A 7
		{
			A_StopSound(5);
			A_StartSound("halflife/monsters/houndeye/attackblast");
			for(int i=0;i<360;i++)
			{
				A_SpawnProjectile("HoundEyeShockWFast",0,0,i,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
				A_SpawnProjectile("HoundEyeShockWSlow",0,0,i,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE);
			}
		}
		Goto See;
	Pain:
		HLP1 A 1
		{
			A_StopSound(5);
			A_Pain();
		}
		HLP1 BCDEFGH 1;
		Goto See;
	Death:
		HLD1 A 1
		{
			A_StopSound(5);
			A_Scream();
		}
		HLD1 BCDE 1;
		HLD1 F 1 A_NoBlocking;
		HLD1 GHI 1;
		HLD1 J 1 A_StartSound("halflife/weapons/bodydrop");
		HLD1 KLMNOPQRSTUVWXYZ[\ 1;
		HLD1 ] -1;
		Stop;
	XDeath:
	Crush:
		TNT1 A 0 A_HLGibs(14,"XenGibs");
		Stop;
	}
}

class HoundEyeShockWFast : Actor
{
	Default
	{
		Speed 19;
		DamageFunction (1);
		Radius 12;
		Height 16;
		RenderStyle "Translucent";
		Alpha .3;
		PROJECTILE;
		+DROPOFF
		-NoGravity
		+FloorHugger
		+Ripper
		Scale .4;
	}
	States
	{
	Spawn:
		HSHK ABC 3;
	Death:
		HSHK C 0;
		Stop;
	}
}

class HoundEyeShockWSlow : HoundEyeShockWFast
	{Default{Speed 12;}}