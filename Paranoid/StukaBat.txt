class HLBat : HLMonster
{
	Default
	{
		Health 200;
		Speed 3;
		FloatSpeed 1;
		Height 40;
		Radius 50;
		Mass 400;
		PainChance 160;
		PainChance "Crowbar",255;
		Scale 1.2;
		+FLOAT
		+NOGRAVITY
		BloodColor "DE FE 72";
		SeeSound "stukabat/sight";
		PainSound "stukabat/pain";
		DeathSound "stukabat/death";
		ActiveSound "stukabat/active";
		AttackSound "stukabat/attack";
		Obituary "%o was fried by a StukaBat.";
		Species "Xen";
	}
	States
	{
	Spawn:
		HLI1 ABCDEFGH 2 A_Look;
		Loop;
	See:
		HLW1 A 1 A_Chase;
		HLW1 BCD 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 E 1 A_Chase;
		HLW1 FGH 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 I 1 A_Chase;
		HLW1 JKL 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 M 1 A_Chase;
		HLW1 NOP 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Q 1 A_Chase;
		HLW1 RST 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 U 1 A_Chase;
		HLW1 VWX 1 A_Chase(null,null,HLSMOOTHCHASE);
		HLW1 Y 1 A_Chase;
		HLW1 Z 1 A_Chase(null,null,HLSMOOTHCHASE);
		Loop;
	Missile:
		HLA1 ABCDEF 2 A_FaceTarget;
		HLA1 G 2 A_HLStukaFire;
		HLA1 HIJKLMN 2;
		Goto See;
	Pain:
		HLP1 ABCDEFGHI 1;
		Goto See;
	Death:
		HLD1 A 0 A_Scream;
		HLD1 ABCDEFGHIJKLMNOPQRSTU 2;
		Goto Death+1;
	Crash:
		HLD1 V 3
		{
			A_PlaySound("halflife/weapons/bodydrop");
			A_NoBlocking();
		}
		HLD1 WXYZ[\] 3;
		HLD2 ABCDEFGHIJKLMNOPQRSTUVWXYZ[\] 3;
		HLD3 AB 3;
		HLD3 C -1;
		Stop;
	XDeath:
	Crush:
	  	TNT1 A 0 A_HLGibs(14,"XenGibs");
		Stop;
	}
	virtual void A_HLStukaFire()
	{
		for(int i=0;i<7;i++)
			A_SpawnItemEx("SBFireExtra",0,0,20,random(2,5),random(2,-2),random(1,3));
		A_SpawnProjectile("StukaFireBall");
	}
}
class HLBossBat : HLBat
{
	Default
	{
		Health 1600;
		Speed 6;
		Height 80;
		Radius 100;
		Mass 600;
		PainChance 30;
		PainChance "Crowbar",135;
		Scale 2;
		SeeSound "bossbat/sight";
		PainSound "bossbat/pain";
		DeathSound "bossbat/death";
		ActiveSound "bossbat/active";
		AttackSound "bossbat/attack";
		Obituary "%o was iced by a Boss Bat.";
	}
	override void A_HLStukaFire()
	{
		A_SpawnProjectile("IceBall",32,-24);
		A_SpawnProjectile("IceBall",32,24);
		A_SpawnProjectile("IceBall",8);
	}
}

class StukaFireBall : Actor
{
	Default
	{
		Projectile;
		Radius 6;
		Height 6;
		Speed 25;
		Scale .35;
		Damage 8;
		RenderStyle "Add";
		Alpha .9;
		Translation "80:111=16:47";
		DeathSound "halflife/weapons/hl2explo1";
	}
	States
	{
	Spawn:
		X022 ABCD 4;
		Loop;
	Death:
		TNT1 A 0
		{
			for(int i=0;i<4;i++)
			{
				bool num;
				actor ball;
				[num,ball]=A_SpawnItemEx("SBFireExtra",0,0,0,random(1,-1),random(1,-1),random(0,2),0,SXF_TRANSFERTRANSLATION);
				ball.scale=(scale.x*.5,scale.y*.5);
			}
		}
		Stop;
	}
}

class SBFireExtra : StukaFireBall
{
	Default
	{
		Scale .26;
		+NOCLIP
		-NOGRAVITY
		Gravity .125;
		Damage 0;
		DeathSound "";
	}
	States
	{
	Spawn:
		X023 AB 3;
		TNT1 A 0 A_Jump(128,"FadeOut");
		Loop;
	FadeOut:
		X023 AB 3 A_FadeOut;
		Loop;
	}
}

class IceBall : StukaFireBall
{
	Default
	{
		Radius 20;
		Height 20;
		Speed 20;
		Scale 1;
		Damage 10;
		Translation "0:255=%[0.0,0.0,0.0]:[0.25,1.0,1.5]";
	}
}